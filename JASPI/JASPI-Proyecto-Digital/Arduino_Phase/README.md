# Fase ARDUINO

Esta carpeta contiene la implementaci칩n y pruebas del sistema de reloj digital usando un m칩dulo RTC DS3231 y una pantalla de 7 segmentos de 4 d칤gitos.

## Tabla de contenido

1. [Estructura](#estructura)
2. [Componentes utilizados](#-componentes-utilizados)
3. [Descripci칩n del funcionamiento](#descripci칩n-del-funcionamiento)
    1. [`Arduino-bin.ino`](#arduino-binino)
    2. [`reloj_con_7_Segmentos.ino`](#reloj_con_7_segmentosino)
4. [Imagen del montaje](#imagen-del-montaje)
5. [Notas adicionales](#notas-adicionales)

---

## Estructura

- `Arduino-bin/Arduino-bin.ino`  
  C칩digo que obtiene la hora desde el m칩dulo RTC y la transmite en binario a trav칠s del monitor serial.

- `reloj_con_7_Segmentos/reloj_con_7_Segmentos.ino`  
  C칩digo para mostrar la hora en un display de 7 segmentos usando multiplexaci칩n.

- `IMG_20250618_190723.jpg`  
  Imagen del montaje f칤sico del sistema Arduino.

---

## 游댢 Componentes utilizados

- Arduino UNO
- M칩dulo RTC DS3231
- Display de 4 d칤gitos, 치nodo com칰n
- Resistencias de 220풜
- Cables de conexi칩n

---

## Descripci칩n del funcionamiento

### `Arduino-bin.ino`

Este archivo permite visualizar los valores de la hora en formato binario por el monitor serial.  
Se extraen los d칤gitos de las horas y minutos, y se convierten en binario usando la funci칩n `bitRead` (Se implementa en la FPGA).

```

#include <Wire.h>
#include <RTClib.h>

RTC_DS3231 rtc;

void printBin(uint8_t value, uint8_t bits = 4) {
  for (int8_t i = bits - 1; i >= 0; --i)
    Serial.print(bitRead(value, i));
}

void setup() {
  Serial.begin(9600);
  while (!Serial);
  Wire.begin();
  rtc.begin();
}

void loop() {
  DateTime now = rtc.now();

  uint8_t h1 = now.hour()   / 10;
  uint8_t h2 = now.hour()   % 10;
  uint8_t m1 = now.minute() / 10;
  uint8_t m2 = now.minute() % 10;

  printBin(h1); Serial.print(" ");
  printBin(h2); Serial.print(":");
  printBin(m1); Serial.print(" ");
  printBin(m2); Serial.println();
}

```

### `reloj_con_7_Segmentos.ino`

Este archivo muestra la hora en un display de 4 d칤gitos utilizando multiplexaci칩n.
Usa un mapa de segmentos para representar cada n칰mero y un sistema de refresco r치pido para activar un d칤gito a la vez.


```

#include <Wire.h>
#include <RTClib.h>

RTC_DS3231 rtc; // Se usa 치nodo com칰n

const uint8_t segPins[7]  = {2, 3, 4, 5, 6, 7, 8};   // a b c d e f g
const uint8_t digPins[4]  = {9, 10, 11, 12};         // d칤gitos

const uint8_t segMap[10] = {
  0b00111111, 0b00000110, 0b01011011, 0b01001111,
  0b01100110, 0b01101101, 0b01111101, 0b00000111,
  0b01111111, 0b01101111
};

uint8_t digits[4];
unsigned long tSec = 0;

void setSegments(uint8_t pattern) {
  for (uint8_t s = 0; s < 7; s++) {
    bool bit = (pattern >> s) & 0x01;
    digitalWrite(segPins[s], bit ? LOW : HIGH);
  }
}

void refreshDisplay() {
  for (uint8_t d = 0; d < 4; d++) {
    digitalWrite(digPins[d], HIGH);
    setSegments(segMap[digits[d]]);
    delayMicroseconds(2000);
    digitalWrite(digPins[d], LOW);
  }
}

void setup() {
  for (uint8_t i = 0; i < 7; i++) pinMode(segPins[i], OUTPUT);
  for (uint8_t i = 0; i < 4; i++) pinMode(digPins[i], OUTPUT);

  Wire.begin();
  rtc.begin();
}

void loop() {
  DateTime now = rtc.now();

  digits[0] = now.hour() / 10;
  digits[1] = now.hour() % 10;
  digits[2] = now.minute() / 10;
  digits[3] = now.minute() % 10;

  refreshDisplay();
}

```
## Imagen del montaje

<p align="center"> 
    <img src="IMG_20250618_190723.jpg" width="400" alt="Montaje del sistema Arduino con RTC y 7 segmentos"/> 
</p>

## Notas adicionales

- Hay que asegurar que el display sea 치nodo com칰n. Se identific칩 un error inicial por suponer que era c치todo com칰n.
- Las conexiones deben respetar el orden de los pines segPins y digPins.
- El multiplexado evita el uso de m칰ltiples controladores, optimizando el uso de pines en el Arduino.
